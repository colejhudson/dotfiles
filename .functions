#!/usr/bin/env fish

function update-gf -a MESSAGE
  set URL https://whatthefuckisyourboyfrienddoing.now.sh/
  curl --request POST --data $MESSAGE $URL
end

function markdown-preview
  set FILE (mktemp).html
  pandoc -o $FILE $argv[1]
  open $FILE
end

function gcc-run
  set file (mktemp)
  echo "gcc $argv -o $file"
  gcc $argv -o $file; and eval $file
  rm $file
end

function nb-rocm-tensorflow
  docker run \
    -d \
    -w $PWD \
    -p 0:8888 \
    --user root \
    -v $PWD:$PWD \
    --device=/dev/kfd \
    --device=/dev/dri \
    --group-add video \
    -e GRANT_SUDO=yes \
    --cap-add=SYS_PTRACE \
    --entrypoint='jupyter' \
    -e JUPYTER_ENABLE_LAB=yes \
    --security-opt seccomp=unconfined \
    rocm/tensorflow \
      notebook \
      --port=8888 \
      --allow-root \
      --no-browser \
      --ip=0.0.0.0 \
      $argv
end

function nb-datascience
  docker run \
    -d \
    --user root \
    -w $PWD \
    -v $PWD:$PWD \
    -p 0:8888 \
    -e GRANT_SUDO=yes \
    -e JUPYTER_ENABLE_LAB=yes \
    jupyter/datascience-notebook \
      jupyter lab \
      --no-browser \
      --ip=0.0.0.0 \
      --port=8888 \
      --allow-root \
      $argv
end

function nb-sagemath
  docker run \
    -d \
    --user root \
    -w $PWD \
    -v $PWD:$PWD \
    -p 0:8888 \
    -e GRANT_SUDO=yes \
    -e JUPYTER_ENABLE_LAB=yes \
    --entrypoint='sage' \
    sagemath/sagemath-jupyter \
      -n jupyter \
      --no-browser \
      --ip=0.0.0.0 \
      --port=8888 \
      --allow-root \
      $argv
end

function nb-arguments
  set CMD $argv[1]
  set --erase argv[1]

  switch $CMD
    case ds datasci datascience
      nb-datascience $argv
    case sm sage sagemath
      nb-sagemath $argv
    case tf tensor tensorflow rocm-tensorflow rocm
      nb-rocm-tensorflow $argv
  end
end

function nb
  set DOCKER_ID (nb-arguments $argv)
  
  while not docker logs $DOCKER_ID 2>&1 | grep http | head -n 1 | cut -d '?' -f 2 | grep token 2>&1 1>/dev/null
    sleep 0.1
  end
  
  printf "Done! Opening Jupyter Labs..."
  
  set PORT (docker container inspect $DOCKER_ID | jq '.[0]["NetworkSettings"]["Ports"]["8888/tcp"][0]["HostPort"]' | cut -d '"' -f 2) 
  set PARAMS (docker logs $DOCKER_ID 2>&1 | grep http | head -n 1 | cut -d '?' -f 2)
  open "http://localhost:"$PORT"/?"$PARAMS
end

function docker-most-recent
  docker ps | awk '{ print $1 }' | head -n 2 | tail -n 1
end

function docker-stop-most-recent
  docker stop (docker-most-recent)
end

function google
  set QUERY (string join '+' (string split ' ' $argv))
  open "https://www.google.com/search?q=$QUERY"
end
